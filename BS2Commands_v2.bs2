' {$STAMP BS2}
' {$PBASIC 2.5}

' ##### Declare the Command Constants #####

' ~ Speech Commands
CMD_Mouth_Pin2_ON     CON   21
CMD_Mouth_Pin2_OFF    CON   20
CMD_Mouth_Pin1_ON     CON   11
CMD_Mouth_Pin1_OFF    CON   10
CMD_Mouth_Pin4_ON     CON   41
CMD_Mouth_Pin4_OFF    CON   40
CMD_Mouth_Pin5_ON     CON   51
CMD_Mouth_Pin5_OFF    CON   50

CMD_Mouth_Pos_OP1     CON   91
CMD_Mouth_Pos_OP2     CON   92
CMD_Mouth_Pos_OP3     CON   93
CMD_Mouth_Pos_OP4     CON   94
CMD_Mouth_Pos_NAR     CON   95
CMD_Mouth_Pos_MED     CON   96
CMD_Mouth_Pos_SAD     CON   99
CMD_Mouth_Pos_CLO     CON   90

' ~ Movement Commands
CMD_Move_Forward      CON  111
CMD_Move_Backward     CON  100
CMD_Move_Rotate_Left  CON  101
CMD_Move_Rotate_Right CON  110
CMD_Move_BREAK        CON  199

' ##### Initialize Runtime #####
BAUD                  CON   84
EXEC_Interval         CON   15
WAIT_Interval         CON   500
INTERVAL              VAR   Word
COMDATA               VAR   Word

' ##### Initialize Speech #####
HIGH 2: PAUSE 100
HIGH 1: PAUSE 100  ' Light them all up in sequence
HIGH 4: PAUSE 100  ' to verify connections and then
HIGH 5: PAUSE 400  ' switch them all off!

LOW 2: LOW 1: LOW 4: LOW 5

' ##### Initialize Pan and Tilt #####
PIN_PanTilt_Pan       PIN   13
POS_PanTilt_Pan       VAR   Word
CMD_PanTilt_Pan       VAR   Word
SPEED_PanTilt_Pan     VAR   Word

PIN_PanTilt_Tilt      PIN   12
POS_PanTilt_Tilt      VAR   Word
CMD_PanTilt_Tilt      VAR   Word
SPEED_PanTilt_Tilt    VAR   Word

' ~ Pan and Tilt Commands
'
' Command Arguments >= 1000 are reserved for Pan and Tilt Commands:
' The Constant value (10 to 150) times 10 is equal to the PULSOUT rate
' which determines the servo position.
'
' The First Digit determines which servo - 1: Pan, 2: Tilt, e.g.:
' 1075 - Centers the Pan servo
' 2075 - Centers the Tilt servo
' 1010 - Rotates the head all the way to the Left
' 1150 - Rotates the head all the way to the Right
' 2010 - Tilts the head all the way to the Front
' 2150 - Tilts the head all the way Back
'
' 1901 - 1910: Sets the Pan servo speed 1 through 10
' 2901 - 2910: Sets the Tilt servo speed 1 through 10

' Center the Servos
CMD_PanTilt_Pan    = 750
POS_PanTilt_Pan    = 750
SPEED_PanTilt_Pan  = 5

CMD_PanTilt_Tilt   = 750
POS_PanTilt_Tilt   = 750
SPEED_PanTilt_Tilt = 5

' ##### Loop to get the Commands #####
DO

  SERIN 16, BAUD, INTERVAL, PanTiltOutput, [DEC COMDATA]

  'DEBUG CLS, "Awaiting Command..."        ' prompt user
  'DEBUGIN DEC COMDATA              ' retrieve number in any format

  IF COMDATA > 0 THEN
    DEBUG "Command: ", DEC COMDATA, CR
  ENDIF

  IF COMDATA < 100 THEN

  ' !-- Execute a Speech Command
    SELECT COMDATA
      CASE CMD_Mouth_Pin2_ON:  HIGH 2
      CASE CMD_Mouth_Pin2_OFF: LOW  2
      CASE CMD_Mouth_Pin1_ON:  HIGH 1
      CASE CMD_Mouth_Pin1_OFF: LOW  1
      CASE CMD_Mouth_Pin4_ON:  HIGH 4
      CASE CMD_Mouth_Pin4_OFF: LOW  4
      CASE CMD_Mouth_Pin5_ON:  HIGH 5
      CASE CMD_Mouth_Pin5_OFF: LOW  5
      CASE CMD_Mouth_Pos_OP1:  HIGH 2: LOW  1: LOW  4: LOW  5
      CASE CMD_Mouth_Pos_OP2:  HIGH 2: HIGH 1: LOW  4: LOW  5
      CASE CMD_Mouth_Pos_OP3:  HIGH 2: HIGH 1: LOW  4: HIGH 5
      CASE CMD_Mouth_Pos_OP4:  HIGH 2: HIGH 1: HIGH 4: LOW  5
      CASE CMD_Mouth_Pos_NAR:  HIGH 2: LOW  1: LOW  4: HIGH 5
      CASE CMD_Mouth_Pos_MED:  HIGH 2: HIGH 1: HIGH 4: HIGH 5
      CASE CMD_Mouth_Pos_CLO:  LOW  2: LOW  1: LOW  4: LOW  5
      CASE CMD_Mouth_Pos_SAD:  LOW  2: HIGH 1: LOW  4: HIGH 5
    ENDSELECT

  ELSEIF COMDATA < 1000 THEN

  ' !-- Execute a Movement Command


  ELSE

  ' !-- Execute a Pan or Tilt Command
    IF COMDATA < 2000 THEN

        CMD_PanTilt_Pan = 10 * (COMDATA - 1000)

        DEBUG "Panning to Position ", DEC CMD_PanTilt_Pan, CR
    ELSEIF COMDATA < 3000 THEN

        CMD_PanTilt_Tilt = 10 * (COMDATA - 2000)

        DEBUG "Tilting to Position ", DEC CMD_PanTilt_Tilt, CR
    ENDIF

  ENDIF

' ##### Output Command Data to Servos #####

PanTiltOutput:

  'DEBUG "Pan Position " : DEBUG POS_PanTilt_Pan
  'DEBUG "Tilt Position " : DEBUG POS_PanTilt_Tilt

  IF POS_PanTilt_Pan <> CMD_PanTilt_Pan OR POS_PanTilt_Tilt <> CMD_PanTilt_Tilt THEN

    ' !-- Output to Pan or Tilt Servos
    IF POS_PanTilt_Pan < CMD_PanTilt_Pan THEN
      POS_PanTilt_Pan = POS_PanTilt_Pan + SPEED_PanTilt_Pan
    ENDIF
    IF POS_PanTilt_Pan > CMD_PanTilt_Pan THEN
      POS_PanTilt_Pan = POS_PanTilt_Pan - SPEED_PanTilt_Pan
    ENDIF
    IF POS_PanTilt_Tilt < CMD_PanTilt_Tilt THEN
      POS_PanTilt_Tilt = POS_PanTilt_Tilt + SPEED_PanTilt_Tilt
    ENDIF
    IF POS_PanTilt_Tilt  > CMD_PanTilt_Tilt THEN
      POS_PanTilt_Tilt = POS_PanTilt_Tilt - SPEED_PanTilt_Tilt
    ENDIF

    'DEBUG "Command: ", DEC CMD_PanTilt_Pan, ",", DEC CMD_PanTilt_Tilt, CR
    'DEBUG "Position: ", DEC POS_PanTilt_Pan, ",", DEC POS_PanTilt_Tilt, CR

    INTERVAL = EXEC_INTERVAL
  ELSE

    INTERVAL = WAIT_INTERVAL
  ENDIF

  PULSOUT PIN_PanTilt_Pan, POS_PanTilt_Pan
  PULSOUT PIN_PanTilt_Tilt, POS_PanTilt_Tilt

  COMDATA = 0                              ' Reset the Command Data
LOOP